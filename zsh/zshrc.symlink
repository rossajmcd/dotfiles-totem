# home
export ROOT=~/

# shortcut to this dotfiles-totem path is $ZSH
export ZSH=$HOME/.dotfiles-totem

# use .totem-localrc for SUPER SECRET stuff that you don't
# want in your public, versioned repo.
if [[ -a ~/.totem-localrc ]]
then
  source ~/.totem-localrc
fi

# use .totem-localzsh for customised .dotfiles-totem extensions for
# different environments, where you do not want your
# secrets exposed over your VCS repo
if [[ -a ~/.totem-localzsh ]]
then
  source ~/.totem-localzsh
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh:#*/*.1}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

bindkey -e

export PATH="$PATH:$HOME/.rvm/bin" # Add RVM to PATH for scripting
